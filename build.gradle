plugins {
    id 'java-library'
    id 'maven-publish'
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

archivesBaseName = 'hummingbird'
group 'com.sparrowwallet'
version '1.7.3'

repositories {
    mavenCentral()
}

dependencies {
    implementation('co.nstant.in:cbor:0.9')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes  'Automatic-Module-Name' : 'com.sparrowwallet.hummingbird'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name.set("hummingbird")
                description.set("Java implementation of Uniform Resources (UR)")
                url.set("https://github.com/sparrowwallet/hummingbird")
                licenses {
                    license {
                        name.set("The Apache Software License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }
                developers {
                    developer {
                        id.set("craigraw")
                        name.set("Craig Raw")
                        email.set("mail@sparrowwallet.com")
                    }
                }
                scm {
                    connection.set("scm:https://github.com/sparrowwallet/hummingbird.git")
                    developerConnection.set("scm:git@github.com:sparrowwallet/hummingbird.git")
                    url.set("https://github.com/sparrowwallet/hummingbird")
                }
            }
        }
    }
}

nexusPublishing {
    packageGroup = "com.sparrowwallet.hummingbird"
    repositories {
        sonatype {
            stagingProfileId = "a63b4b5e2bff9"
            username = project.findProperty("nexusUsername")
            password = project.findProperty("nexusPassword")
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

/* ./gradlew publishToSonatype closeAndReleaseStagingRepository */